@module bios_service

@import term

SERVICE_REBOOT u8 <- 0x00
SERVICE_SET_VIDEO_MODE u8 <- 0x01
SERVICE_READ_SECTORS u8 <- 0x02
SERVICE_SECTORS_COUNT u8 <- 0x03

@export VIDEO_MODE_TEXT_80x25 u8 <- 0x00
@export VIDEO_MODE_TEXT_80x50 u8 <- 0x01
@export VIDEO_MODE_GPX_320x200x8 u8 <- 0x02
@export VIDEO_MODE_GPX_640x480x4 u8 <- 0x03

serviceAdr u32

@export init fun: biosServiceAdr u32
    serviceAdr <- biosServiceAdr
;

rebootRaw raw<"m,m,~{eax}">: biosServiceAdr u32, serviceNum u8
    mov ah, $1
    call $0
;

@export reboot fun
    rebootRaw(serviceAdr, SERVICE_REBOOT)
;

readSectorsRaw raw<"m, m, m, m, m, m,~{eax},~{ebx},~{ecx},~{edi}">: biosServiceAdr u32, serviceNum u8, driveNumber u8, firstSectorNum u32, sectorsCount u32, targetAddress u32
    mov ah, $1
    mov al, $2
    mov ebx, $3
    mov ecx, $4
    mov edi, $5
    call $0
;

@export readSectors fun: driveNum u32, firstSectorNum u32, sectorsCount u32, targetAddress ptr<u8>
    readSectorsRaw(serviceAdr, SERVICE_READ_SECTORS, driveNum.u8, firstSectorNum, sectorsCount, targetAddress.vAdr)
;

sectorsCountRaw raw<"=r,m,m,m,~{eax},~{edx}">: biosServiceAdr u32, serviceNum u8, driveNum u8 -> u32
    mov ah, $2
    mov dl, $3
    call $1
    mov $0, eax
;

@export sectorsCount fun: driveNum u32 -> u32
    ret sectorsCountRaw(serviceAdr, SERVICE_SECTORS_COUNT, driveNum)
;

setVideoModeRaw raw<"m, m, m">: biosServiceAdr u32, serviceNum u8, modeNum u8
    mov ah, $1
    mov al, $2
    call $0
;

@export setVideoMode fun: videoMode u8
    setVideoModeRaw(serviceAdr, SERVICE_SET_VIDEO_MODE, videoMode)
;